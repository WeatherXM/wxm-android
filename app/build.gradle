plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.firebase-perf"
}

def getGitCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.weatherxm.android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "3.0.0-alpha"

        // BuildConfig fields
        buildConfigField("String", "API_URL", "\"$API_URL\"")
        buildConfigField("String", "AUTH_URL", "\"$AUTH_URL\"")

        // Resource value fields
        resValue("string", "mapbox_access_token", "$MAPBOX_ACCESS_TOKEN")
    }

    signingConfigs {
        release {
            storeFile file("$SIGNING_KEYSTORE")
            storePassword "$SIGNING_KEYSTORE_PASSWORD"
            keyAlias "$SIGNING_KEY_ALIAS"
            keyPassword "$SIGNING_KEY_PASSWORD"
        }
    }

    flavorDimensions "mode"

    productFlavors {
        mock {
            dimension "mode"
        }
        remote {
            dimension "mode"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            minifyEnabled false // change it to true if you want to test the obfuscation on debug mode
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        viewBinding true
    }

    // Filter out specific build variants
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("remote") && names.contains("mock")) {
            variant.setIgnore(true)
        } else if (variant.buildType.name != "debug" && names.contains("mock")) {
            variant.setIgnore(true)
        }
    }

    // Flavor-specific version name & apk file name, also delete mock files on release APK flavor
    applicationVariants.all { variant ->
        def version = "${variant.getVersionName()}-$getGitCommitHash-${variant.buildType.name}"
        variant.resValue "string", "app_version", "$version"

        variant.outputs.all { output ->
            output.versionNameOverride = version
            outputFileName = "weatherxm-${version}.apk"
        }
        if (variant.buildType.name.contains('release')) {
            variant.mergeAssetsProvider.get().doLast {
                delete(fileTree(dir: variant.mergeAssetsProvider.get().outputDir, includes: ['**/mock/*']))
            }
        }
    }

    compileOptions {
        // Enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude "META-INF/*"
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    // Desugaring for Java8 feature support
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // Android Jetpack libraries
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.annotation:annotation:1.3.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.collection:collection-ktx:1.1.0"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.1"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"

    // Google Play Services for Location & Maps
    implementation "com.google.android.gms:play-services-location:18.0.0"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // JSON serialization/deserialization
    implementation "com.squareup.moshi:moshi:1.12.0"
    implementation "com.squareup.moshi:moshi-adapters:1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

    // HTTP client
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.2")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Network response wrapper
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.2.2"

    // JWT auth utilities
    implementation "com.auth0.android:jwtdecode:2.0.1"

    // Animations
    implementation "com.airbnb.android:lottie:4.2.0"

    // Charts
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // Permissions
    implementation "com.github.fondesa:kpermissions:3.2.1"

    // Dependency Injection
    implementation "io.insert-koin:koin-android:3.1.3"

    // Data types and more
    implementation platform("io.arrow-kt:arrow-stack:1.0.1")
    implementation "io.arrow-kt:arrow-core"
    implementation "io.arrow-kt:arrow-fx-coroutines"

    // Mapbox
    implementation "com.mapbox.maps:android:10.1.0"

    // Insetter
    implementation "dev.chrisbanes.insetter:insetter:0.6.0"

    // Import the Firebase BoM
    implementation platform("com.google.firebase:firebase-bom:29.0.0")
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"

    // QR Code Scanner
    implementation "com.journeyapps:zxing-android-embedded:4.3.0"

    // Retromock for mocking responses
    implementation "co.infinum:retromock:1.1.1"
}
