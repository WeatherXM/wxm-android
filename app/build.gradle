plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.appdistribution"
}

def getGitCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.weatherxm.android"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 1
        versionName "3.0.0-alpha"

        // Resource value fields
        resValue("string", "mapbox_access_token", "$MAPBOX_ACCESS_TOKEN")
    }

    signingConfigs {
        base {
            storeFile file("$SIGNING_KEYSTORE")
            storePassword "$SIGNING_KEYSTORE_PASSWORD"
            keyAlias "$SIGNING_KEY_ALIAS"
            keyPassword "$SIGNING_KEY_PASSWORD"
        }
    }

    flavorDimensions "mode", "server"

    productFlavors {
        local {
            dimension "mode"
        }
        remote {
            dimension "mode"
        }
        mock {
            dimension "server"
            resValue "string", "app_name", "WXM Mock"
            buildConfigField("String", "API_URL", "\"https://api-dev.weatherxm.com\"")
            buildConfigField("String", "AUTH_URL", "\"https://api-dev.weatherxm.com\"")
        }
        dev {
            dimension "server"
            resValue "string", "app_name", "WXM Dev"
            buildConfigField("String", "API_URL", "\"https://api-dev.weatherxm.com\"")
            buildConfigField("String", "AUTH_URL", "\"https://api-dev.weatherxm.com\"")
        }
        prod {
            dimension "server"
            resValue "string", "app_name", "WeatherXM"
            buildConfigField("String", "API_URL", "\"https://api.weatherxm.com\"")
            buildConfigField("String", "AUTH_URL", "\"https://api.weatherxm.com\"")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.base
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            firebaseAppDistribution {
                appId = "$RELEASE_APP_ID"
                artifactType = "APK"
                releaseNotes = "Release notes for development version"
                serviceCredentialsFile = "${rootDir.path}/ci-service-account.json"
                groups = "$FIREBASE_PUBLIC_TEST_GROUP"
            }
        }
        debug {
            applicationIdSuffix = ".debug"
            signingConfig signingConfigs.base
            minifyEnabled false
            // change it to true if you want to test the obfuscation on debug mode
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            firebaseAppDistribution {
                appId = "$DEBUG_APP_ID"
                artifactType = "APK"
                releaseNotes = "Release notes for development version"
                serviceCredentialsFile = "${rootDir.path}/ci-service-account.json"
                groups = "$FIREBASE_INTERNAL_TEST_GROUP"
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    // Filter out specific build variants
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("local") && !names.contains("mock")) {
            variant.setIgnore(true)
        } else if (names.contains("remote") && names.contains("mock")) {
            variant.setIgnore(true)
        } else if (variant.buildType.name != "debug" && names.contains("mock")) {
            variant.setIgnore(true)
        }
    }

    // Flavor-specific version name & apk file name, also
    applicationVariants.all { variant ->
        // Base version
        def version = "${variant.getVersionName()}-$getGitCommitHash"

        // Add flavor in version, if mock or dev
        def flavor = variant.getFlavorName().toLowerCase()
        if (flavor.contains("mock")) {
            version = "$version-mock"
        } else if (flavor.contains("dev")) {
            version = "$version-dev"
        }

        // Add build type in version
        version = "$version-${variant.buildType.name}"

        // Create app_version res string
        variant.resValue "string", "app_version", "$version"

        // Proper apk file name
        variant.outputs.all { output ->
            output.versionNameOverride = version
            outputFileName = "weatherxm-${version}.apk"
        }

        // Delete mock files on release APK flavor
        if (variant.buildType.name.contains("release")) {
            variant.mergeAssetsProvider.get().doLast {
                delete(fileTree(dir: variant.mergeAssetsProvider.get().outputDir, includes: ["**/mock/*"]))
            }
        }
    }

    compileOptions {
        // Enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        exclude "META-INF/*"
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    // Desugaring for Java8 feature support
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // Android Jetpack libraries
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.annotation:annotation:1.3.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.collection:collection-ktx:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.0"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"
    implementation "androidx.security:security-crypto:1.0.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.5.0"

    // Google Play Services for Location & Maps
    implementation "com.google.android.gms:play-services-location:19.0.1"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // JSON serialization/deserialization
    implementation "com.squareup.moshi:moshi:1.13.0"
    implementation "com.squareup.moshi:moshi-adapters:1.13.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"

    // HTTP client
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.3")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Network response wrapper
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.2.2"

    // JWT auth utilities
    implementation "com.auth0.android:jwtdecode:2.0.1"

    // Animations
    implementation "com.airbnb.android:lottie:4.2.1"

    // Charts
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"
    implementation "com.diogobernardino:williamchart:3.10.1"

    // Permissions
    implementation "com.github.fondesa:kpermissions:3.2.1"

    // Dependency Injection
    implementation "io.insert-koin:koin-android:3.1.5"

    // Data types and more
    implementation platform("io.arrow-kt:arrow-stack:1.0.1")
    implementation "io.arrow-kt:arrow-core"
    implementation "io.arrow-kt:arrow-fx-coroutines"

    // Mapbox
    implementation "com.mapbox.maps:android:10.2.0"

    // Insetter
    implementation "dev.chrisbanes.insetter:insetter:0.6.1"

    // Import the Firebase BoM
    implementation platform("com.google.firebase:firebase-bom:29.0.4")
    implementation "com.google.firebase:firebase-crashlytics-ktx"

    // QR Code Scanner
    implementation "com.journeyapps:zxing-android-embedded:4.3.0"

    // Retromock for mocking responses
    implementation "co.infinum:retromock:1.1.1"

    // Viewpager
    implementation "androidx.viewpager2:viewpager2:1.0.0"
}
